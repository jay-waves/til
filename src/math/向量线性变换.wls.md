[矩阵运算](../../Math/线性代数/矩阵运算.md) 中的图片: ![|300](../../attach/线性代数_线性变换.avif)

```mathematica

(* A = [alpha, beta] *)  
alpha = {2, 1};  
beta = {1, 2};  
A = Transpose[{alpha, beta}];  
  
customBlue = RGBColor[0.4, 0.6, 0.8];    
  
(* 交互界面 *)  
Manipulate[  
Module[{xVec, Ax, scaledAlpha, scaledBeta},  
(*  
  input: x= [x1, x2]^T  
  A x = [alpha,beta]x=alpha x1 + beta x2    
*)  
xVec = {x1, x2};  
Ax = A.xVec;  
scaledAlpha = x1*alpha;  
scaledBeta = x2*beta;

Graphics[{  
		(* 原始向量 x *)  
		{Thick, Arrow[{{0, 0}, xVec}]},  
		Text[Style["x", Bold, 14], xVec + {0.3, 0.3}],  
			
		(* 新坐标系下的分量 *)  
		{Dashed, Lighter[customBlue], Arrow[{{0, 0}, scaledAlpha}]},  
		Text[Style["x₁\[CenterDot]\[Alpha]", customBlue, 12], scaledAlpha + {0.3, 0.3}],  
		{Dashed, Lighter[customBlue, 0.5], Arrow[{{0, 0}, scaledBeta}]},  
		Text[Style["x₂\[CenterDot]\[Beta]", customBlue, 12], scaledBeta + {0.3, 0.3}],  
			
		(* 结果向量 Ax *)  
		{customBlue, Thick, Arrow[{{0, 0}, Ax}]},  
		Text[Style["Ax", customBlue, Bold, 14], Ax + {0.3, 0.3}],  
			
		(* 向量加法过程的虚线 *)  
		{Dashed, Gray, Line[{scaledAlpha, Ax}]},  
		{Dashed, Gray, Line[{scaledBeta, Ax}]},  
		{Dashed, Gray, Line[{scaledAlpha, scaledAlpha + scaledBeta}]},  
		{Dashed, Gray, Line[{scaledBeta, scaledAlpha + scaledBeta}]}  
	 },  
	 PlotRange -> {{-5, 10}, {-5, 10}},  
	 Axes -> True,  
	 GridLines -> Automatic,  
	 Background -> White  
	]  
	],  
	(* 输入参数 *)  
	{{x1, 1, "x₁"}, -1, 5},  
	{{x2, 1, "x₂"}, -1, 5}  
]
```
